plugins {
	id 'fabric-loom' version '0.2.3-SNAPSHOT'
	id 'maven-publish'
	id 'java-library'
	id 'java'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = System.getenv("COMMIT") ? "${project.sandbox_version}-${System.getenv("COMMIT")}" : "${project.sandbox_version}"
group = project.maven_group

minecraft {
}

repositories {
	maven { url 'https://cdn.hrzn.studio/maven' }
}

configurations {
	shade
	compile.extendsFrom shade
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}"
	modCompile "net.fabricmc:fabric-loader:${project.loader_version}"
	implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
	shade project(':discord-rpc')

	implementation project(':SandboxAPI')
	if(!System.getenv("COMMIT")) {
		runtime project(':TestMod')
	}
}

jar {
	from {
		configurations.shade.collect { it.isDirectory() ? it : zipTree(it) }
	}
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(jar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
			pom.withXml {
				asNode().dependencies.'*'.findAll() {
					it.artifactId.text() == 'fabric-loader'
				}.each() {
					it.parent().remove(it)
				}
			}
		}
	}
	repositories {
		if(System.getenv("BRANCH") == "master") {
			maven {
				url "s3://cdn.hrzn.studio/maven"
				authentication {
					awsIm(AwsImAuthentication)
				}
			}
		} else {
			mavenLocal()
		}
	}
}
